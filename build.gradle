plugins {
    id 'java'
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)' apply(false)
    id 'org.spongepowered.mixin' version "0.7-SNAPSHOT" apply(false)
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

apply plugin: 'java'

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'idea'
    version = project.modVersion + "-" + minecraftVersion
    group = mavenGroup

}

subprojects {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    repositories {
        mavenCentral()
        maven { url "https://repo.spongepowered.org/repository/maven-public/"}
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }
}

minecraft {
    version(minecraftVersion)
    accessWideners(project.file("src/main/resources/epherolib-root.accesswidener"))
}

configurations {
    shade
    api.extendsFrom shade
}

dependencies {
    compileOnly group:'org.spongepowered', name:'mixin', version: mixinVersion
    compileOnly("org.jetbrains:annotations:${project.JBAnnotationsVersion}")
    shade("org.spongepowered:configurate-hocon:4.1.2")
    shade("org.spongepowered:configurate-core:4.1.2")
    shade("com.typesafe:config:1.4.2")
    shade("io.leangen.geantyref:geantyref:1.3.14")
}

artifacts {
    archives shadowJar
    shadow shadowJar
}

sourceSets {
    main {
        resources {
            srcDirs += [
                    "src/main/generated"
            ]
        }
    }
}

shadowJar {
    archiveClassifier = ""
    enabled = true
    configurations = [project.configurations.shade]
    relocate("org.spongepowered.configurate", "com.epherical.epherolib.libs.org.spongepowered.configurate")
    relocate("com.typesafe", "com.epherical.epherolib.libs.com.typesafe")
    relocate("io.leangen", "com.epherical.epherolib.libs.io.leangen")
}

task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "${modBaseArchiveName}"
            // add all the jars that should be included when publishing to maven
            from (rootProject.components.java)

            artifact sourceJar
        }
    }

    repositories {
        maven {
            name = "EphericalMaven"
            url = "https://maven.epherical.com/releases"
            credentials {
                username = m_user
                password = m_pass
            }
        }
    }
}
